buildscript {
    ext {
        kotlin_version = "1.3.61"
        asoft_test_version = "4.1.0"
        coroutines_version = "1.3.3"
        android_build_tools_version = "3.5.2"
    }

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$android_build_tools_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
    }
}

plugins {
    id 'kotlin-multiplatform' version '1.3.61'
}

tasks.whenTaskAdded { task ->
    if (task.name.contains("install")) {
//        task.enabled = false
    }
}

group 'tz.co.asoft'
version "6.1.0"

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'kotlinx-serialization'

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 1
        targetSdkVersion 28
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main {
            manifest.srcFile 'src/androidMain/AndroidManifest.xml'
            resources.srcDirs = ['src/androidMain/resources']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

repositories {
    google()
    mavenCentral()
    jcenter()
    maven { url "https://kotlin.bintray.com/kotlinx" }
    maven { url 'https://dl.bintray.com/kotlin/kotlin-js-wrappers' }
    maven { url 'https://dl.bintray.com/kotlinx/kotlinx' }
    maven { url "https://jitpack.io" }
    maven { url "https://dl.bintray.com/soywiz/soywiz" }
}


kotlin {
    android() {
        compilations.all {
            tasks[compileKotlinTaskName].kotlinOptions {
                jvmTarget = "1.8"
            }
        }
        publishLibraryVariants("release")
    }

    jvm() {
        compilations.all {
            tasks[compileKotlinTaskName].kotlinOptions {
                jvmTarget = "1.8"
            }
        }
    }

    js() {
//        browser()
//        nodejs()
        compilations.all {
            tasks[compileKotlinTaskName].kotlinOptions {
                kotlinOptions.metaInfo = true
                kotlinOptions.sourceMap = true
                kotlinOptions.moduleKind = 'commonjs'
                kotlinOptions.main = "call"
            }
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$coroutines_version"
            }
        }
        commonTest {
            dependencies {
                implementation "com.github.andylamax.asoft-test:asoft-test-metadata:$asoft_test_version"
            }
        }
        jvmMain {
            dependencies {
                implementation kotlin('stdlib')
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
            }
        }
        jvmTest {
            dependencies {
                implementation "com.github.andylamax.asoft-test:asoft-test-jvm:$asoft_test_version"
            }
        }
        androidMain {
            dependencies {
                implementation kotlin('stdlib')
                api "androidx.lifecycle:lifecycle-livedata:2.0.0"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
            }
        }
        androidTest {
            dependencies {
                implementation "com.github.andylamax.asoft-test:asoft-test-android:$asoft_test_version"
            }
        }
        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-js:$coroutines_version"
            }
        }
        jsTest {
            dependsOn jsMain
            dependencies {
                implementation "com.github.andylamax.asoft-test:asoft-test-js:$asoft_test_version"
            }
        }
    }
}